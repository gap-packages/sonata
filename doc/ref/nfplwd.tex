%
\Chapter{Nearfields, planar nearrings and weakly divisible nearrings}
%


A *nearfield* is a nearring with $1$ where each nonzero element has a
multiplicative inverse. The (additive) group reduct of a finite
nearfield is necessarily elementary abelian. 
For an exposition of nearfields we refer to \cite{Waehling:Fastkoerper}.

Let $(N,+,\cdot)$ be a left nearring. For $a,b \in N$ we define $a \equiv b$
iff $a\cdot n = b\cdot n$ for all $n\in N$. If $a \equiv b$, then $a$ and $b$
are called *equivalent multipliers*.
A nearring $N$ is called *planar* if $| N/_{\equiv} | \ge 3$ and if 
for any two non-equivalent multipliers $a$ and $b$ in $N$, for any $c\in N$, 
the equation $a\cdot x = b\cdot x + c$ has a unique solution. 
See \cite{Clay:Nearrings} for basic results on planar nearrings. 

All finite nearfields are planar nearrings.

A left nearring $(N,+,\cdot)$ is called *weakly divisible* if 
$\forall a,b\in N \exists x\in N : a\cdot x = b$ or $b\cdot x = a$.  

All finite integral planar nearrings are weakly divisible.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Dickson numbers}


\>IsPairOfDicksonNumbers( <q>, <n> )

A pair of Dickson numbers $(q,n)$ consists of a prime power integer $q$
and a natural number $n$ such that for $p = 4$ or $p$ prime, $p|n$ implies
$p|q-1$.

\beginexample
    gap> IsPairOfDicksonNumbers( 5, 4 );
    true
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Dickson nearfields}


\>DicksonNearFields( <q>, <n> )

All finite nearfields with 7 exceptions can be obtained via socalled
coupling maps from finite fields. These nearfields are called Dickson
nearfields.

The multiplication map of such a Dickson nearfield is given by a pair of
Dickson numbers $(q,n)$ in the following way:
 
Let $F = GF(q^n)$ and $w$ be a primitive element of $F$. Let
$H$ be the subgroup of $(F\setminus\{0\},\cdot)$ generated by $w^n$.
Then $\{w^{(q^i-1)/(q-1)}\ |\ 0\leq i\leq n-1 \}$ is a set of coset
representatives of $H$ in $F\setminus\{0\}$.
For $f\in Hw^{(q^i-1)/(q-1)}$ and $x\in F$ define $f*x = f\cdot x^{q^i}$
and $0*x = 0$. Then $*$ is a nearfield multiplication on the additive group
$(F,+)$. 

Note that a Dickson nearfield is not uniquely determined by $(q,n)$, since
$w$ can be chosen arbitrarily. Different choices of $w$ may yield isomorphic 
nearfields.

`DicksonNearFields' returns a list of the non-isomorphic Dickson nearfields
determined by the pair of Dickson numbers $(q,n)$

\beginexample
    gap> DicksonNearFields( 5, 4 );
    [ ExplicitMultiplicationNearRing ( <pc group of size 625 with 
        4 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 625 with 
        4 generators> , multiplication ) ]
\endexample

\>NumberOfDicksonNearFields( <q>, <n> )

`NumberOfDicksonNearFields' returns the number of non-isomorphic Dickson
nearfields which can be obtained from a pair of Dickson numbers $(q,n)$.
This number is given by $\Phi(n)/k$. Here $\Phi(n)$ denotes the number
of relatively prime residues modulo $n$ and $k$ is the multiplicative order 
of $p$ modulo $n$ where $p$ is the prime divisor of $q$.

\beginexample
    gap> NumberOfDicksonNearFields( 5, 4 );
    2
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Exceptional nearfields}


\>ExceptionalNearFields( <q> )

There are 7 finite nearfields which cannot be obtained from finite fields
via a Dickson process. They are of size $p^2$ for
$p = 5, 7, 11, 11, 23, 29, 59$. (There exist 2 exceptional nearfields of size
121.)

`ExceptionalNearFields' returns the list of exceptional nearfields for a given
size <q>.

\beginexample
    gap> ExceptionalNearFields( 25 );
    [ ExplicitMultiplicationNearRing ( <pc group of size 25 with 
        2 generators> , multiplication ) ]
\endexample

\>AllExceptionalNearFields()

There are 7 finite nearfields which cannot be obtained from finite fields
via a Dickson process. They are of size $p^2$ for
$p = 5, 7, 11, 11, 23, 29, 59$. (There exist 2 exceptional nearfields of size
121.)

`AllExceptionalNearFields' without argument returns the list of exceptional
nearfields.

\beginexample
    gap> AllExceptionalNearFields();
    [ ExplicitMultiplicationNearRing ( <pc group of size 25 with 
        2 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 49 with 
        2 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 121 with 
        2 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 121 with 
        2 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 529 with 
        2 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 841 with 
        2 generators> , multiplication ), 
      ExplicitMultiplicationNearRing ( <pc group of size 3481 with 
        2 generators> , multiplication ) ]
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Planar nearrings}


\>PlanarNearRing( <G>, <phi>, <reps> )

A finite *Ferrero pair* is a pair of groups $(N,\Phi)$ where $\Phi$ is a
fixed-point-free automorphism group of $(N,+)$.   

Starting with a Ferrero pair $(N,\Phi)$ we can construct a planar nearring
in the following way, \cite{Clay:Nearrings}:
Select representatives, say $e_{1},\ldots,e_{t}$, for some or all of the
non-trivial orbits of $N$ under $\Phi$. 
Let $C = \Phi(e_1)\cup\ldots\cup\Phi(e_t)$.
For each $x\in N$ we define $a * x = 0$ for $a\in N\setminus C$, and 
$a * x=\phi_{a}(x)$ for $a\in\Phi(e_{i})\subset C$ and $\phi_{a}(e_{i})=a$.
Then $(N,+,*)$ is a (left) planar nearring.

Every finite planar nearring can be constructed from some Ferrero pair 
together with a set of orbit representatives in this way.

`PlanarNearRing' returns the planar nearring on the group <G> determined by 
the fixed-point-free automorphism group <phi> and the list of chosen orbit 
representatives <reps>.

\beginexample
    gap> C7 := CyclicGroup( 7 );;
    gap> i := GroupHomomorphismByFunction( C7, C7, x -> x^-1 );;
    gap> phi := Group( i );;
    gap> orbs := Orbits( phi, C7 );
    [ [ <identity> of ... ], [ f1, f1^6 ], [ f1^2, f1^5 ], 
      [ f1^3, f1^4 ] ]
    gap> # choose reps from the orbits 
    gap> reps := [orbs[2][1], orbs[3][2]];
    [ f1, f1^5 ]
    gap> n := PlanarNearRing( C7, phi, reps );
    ExplicitMultiplicationNearRing ( <pc group of size 7 with 
    1 generator> , multiplication )
\endexample

\>OrbitRepresentativesForPlanarNearRing( <G>, <phi>, <i> )

%For a fixed Ferrero pair distinct choices of representatives may yield 
%isomorphic nearrings. 
Let $(N,\Phi)$ be a Ferrero pair, and let $E = \{ e_{1},\ldots,e_{s} \}$ and
$F = \{ f_{1},\ldots,f_{t} \}$ be two sets of non-zero orbit representatives.
The nearring obtained from $N,\Phi, E$ by the Ferrero construction
(see `PlanarNearRing') is isomorphic to the nearring obtained from $N,\Phi, F$
iff there exists an automorphism $\alpha$ of $(N,+)$ that normalizes $\Phi$
such that
$\{ \alpha(e_{1}),\ldots,\alpha(e_{s}) \} = \{ f_{1},\ldots,f_{t} \}$.

The function `OrbitRepresentativesForPlanarNearRing' 
returns precisely one set of representatives of cardinality <i> for each 
isomorphism class of planar nearrings which can be generated from the 
Ferrero pair ( <G>, <phi> ).

\beginexample
    gap> C7 := CyclicGroup( 7 );;
    gap> i := GroupHomomorphismByFunction( C7, C7, x -> x^-1 );;
    gap> phi := Group( i );;
    gap> reps := OrbitRepresentativesForPlanarNearRing( C7, phi, 2 );
    [ [ f1, f1^2 ], [ f1, f1^5 ] ]
    gap> n1 := PlanarNearRing( C7, phi, reps[1] );;
    gap> n2 := PlanarNearRing( C7, phi, reps[2] );;
    gap> IsIsomorphicNearRing( n1, n2 );
    false
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Weakly divisible nearrings}


\>WdNearRing( <G>, <psi>, <phi>, <reps> )

Every finite (left) weakly divisible nearring $(N,+,\cdot)$ can be constructed
in the following way:

(1) Let $\psi$ be an endomorphism of the group $(N,+)$ such that Ker
$\psi =$ Image $\psi^{r-1}$ for some integer $r, r>0$. (Let $\psi^0 :=$ id.)

(2) Let $\Phi$ be an automorphism group of $(N,+)$ such that
$\psi\Phi\subseteq\Phi\psi$ and $\Phi$ acts fixed-point-free on
$N\setminus$ Image $\psi$.
(That is, for each
$\varphi\in\Phi$ there exists $\varphi'\in\Phi$ such that
$\psi\varphi = \varphi'\psi$ and for all $n\in N\setminus$ Image $\psi$ the 
equality $n^\varphi = n$ implies $\varphi =$ id. Note that our functions
operate from the right just like GAP-mappings do.)

(3) Let $E\subseteq N$ be a complete set of orbit representatives for
$\Phi$ on $N\setminus$ Image $\psi$, such that for all $e_1, e_2\in E$, for all
$\varphi\in\Phi$ and for all $1 \leq i \leq r-1$ the equality
$e_1^{\varphi\psi^i} = e_2^{\psi^i}$ implies $\varphi\psi^i = \psi^i$.

Then for all $n\in N, n\neq 0$, there are $i\geq 0 ,\varphi\in\Phi$ and
$e\in E$ such that $n = e^{\varphi\psi^i}$; furthermore, for fixed $n$, the
endomorphism $\varphi\psi^i$ is independent of the choice of $e$ and
$\varphi$ in the representation of $n$. 
 
For all $x\in N, e\in E,\varphi\in\Phi$ and $i\geq 0$ define $0\cdot x := 0$
and
    $$ e^{\varphi\psi^i}\cdot x := x^{\varphi\psi^i} $$
Then $(N,+,\cdot)$ is a zerosymmetric (left) wd nearring. 

`WdNearRing' returns the wd nearring on the group <G> as defined above
by the nilpotent endomorphism <psi>, the automorphism group <phi> and
a list of orbit representatives <reps> where the arguments fulfill the
conditions (1) to (3).

\beginexample
    gap> C9 := CyclicGroup( 9 );;
    gap> psi := GroupHomomorphismByFunction( C9, C9, x -> x^3 );;
    gap> Image( psi );
    Group([ f2, <identity> of ... ])
    gap> Image( psi ) = Kernel( psi );
    true
    gap> a := GroupHomomorphismByFunction( C9, C9, x -> x^4 );;
    gap> phi := Group( a );;
    gap> Size( phi );
    3
    gap> orbs := Orbits( phi, C9 );
    [ [ <identity> of ... ], [ f2 ], [ f2^2 ], [ f1, f1*f2, f1*f2^2 ],
      [ f1^2, f1^2*f2^2, f1^2*f2 ] ]
    gap> # choose reps from the orbits outside of Image( psi )
    gap> reps := [orbs[4][1], orbs[5][1]];
    [ f1, f1^2 ]
    gap> n := WdNearRing( C9, psi, phi, reps );
    ExplicitMultiplicationNearRing ( <pc group of size 9 with
    2 generators> , multiplication )
\endexample



%%% Local Variables: 
%%% mode: latex
%%% TeX-master: t
%%% End: 
